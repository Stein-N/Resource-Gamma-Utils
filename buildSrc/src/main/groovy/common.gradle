plugins {
    id 'java-library'
    id 'publish-maven'
}

base {
    archivesName = "${mod_id}-${project.name}"
    version = "${minecraft_version}-${version}"
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(java_version)
    withSourcesJar()
    withJavadocJar()
}

idea.module {
    downloadJavadoc = true
    downloadSources = true
}

repositories {
    mavenCentral()
    mavenLocal()

    maven { url 'https://raw.githubusercontent.com/Stein-N/resources/main/maven' }
    maven { url "https://repo.spongepowered.org/maven" }
    maven { url "https://maven.terraformersmc.com/" }
    maven { url "https://api.modrinth.com/maven" }
}

tasks.named('javadoc', Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
}

tasks.withType(GenerateModuleMetadata).configureEach {
    enabled = false
}

jar {
    from(rootProject.file("LICENSE")) {
        rename { "${it}_${mod_name}" }
    }

    manifest {
        attributes([
                'Specification-Title'   : mod_name,
                'Specification-Vendor'  : mod_author,
                'Specification-Version' : project.jar.archiveVersion,
                'Implementation-Title'  : project.name,
                'Implementation-Version': project.jar.archiveVersion,
                'Implementation-Vendor' : mod_author,
                'Built-On-Minecraft'    : minecraft_version
        ])
    }
}

sourcesJar {
    from(rootProject.file('LICENSE')) {
        rename { "${it}_${mod_name}" }
    }
}

processResources {
    var expandProps = [
            // Project related Keys
            "version": version,
            "group": project.group,
            "java_version": java_version,

            // Mod related Keys
            "minecraft_version": minecraft_version,
            "mod_name": mod_name,
            "mod_author": mod_author,
            "mod_id": mod_id,
            "mod_license": mod_license,
            "mod_description": mod_description,

            // Loader Keys
            "fabric_api": fabric_api,
            "fabric_loader": fabric_loader,
            "forge": forge,
            "neoforge": neoforge

    ]

    filesMatching(['pack.mcmeta', 'fabric.mod.json', 'META-INF/neoforge.mods.toml', 'META-INF/mods.toml', '*.mixins.json']) {
        expand expandProps
    }
    inputs.properties(expandProps)
}

// Declare capabilities on the outgoing configurations.
// Read more about capabilities here: https://docs.gradle.org/current/userguide/component_capabilities.html#sec:declaring-additional-capabilities-for-a-local-component
['apiElements', 'runtimeElements', 'sourcesElements', 'javadocElements'].each { variant ->
    configurations."$variant".outgoing {
        capability("$group:${project.name}:$version")
        capability("$group:${base.archivesName.get()}:$version")
        capability("$group:$mod_id-${project.name}-${minecraft_version}:$version")
        capability("$group:$mod_id:$version")
    }
    publishing.publications.configureEach {
        suppressPomMetadataWarningsFor(variant)
    }
}