plugins {
    id 'loader'
    id 'net.neoforged.moddev'
}

dependencies {
    compileOnly "maven.modrinth:iris:1.10.2+1.21.11-neoforge"
}

neoForge {
    version = neoforge

    def at = file('../common/src/main/resources/META-INF/accesstransformer.cfg')
    if (at.exists()) accessTransformers = [at]

    runs {
        configureEach {
            systemProperty('neoforge.enableGameTestNamespaces', mod_id)
            ideName = "NeoForge ${it.name.capitalize()} (${project.path})"
            gameDirectory = file("../common/runs/${it.name.toLowerCase()}")
        }
        client {
            client()
        }
    }
    mods {
        "${mod_id}" { sourceSet sourceSets.main }
    }
}

// Implement mcgradleconventions loader attribute
def loaderAttribute = Attribute.of('io.github.mcgradleconventions.loader', String)
['apiElements', 'runtimeElements', 'sourcesElements', 'javadocElements'].each { variant ->
    configurations.named("$variant") {
        attributes {
            attribute(loaderAttribute, 'neoforge')
        }
    }
}
sourceSets.configureEach {
    [it.compileClasspathConfigurationName, it.runtimeClasspathConfigurationName, it.getTaskName(null, 'jarJar')].each { variant->
        configurations.named("$variant") {
            attributes {
                attribute(loaderAttribute, 'neoforge')
            }
        }
    }
}

publishMods {
    file = jar.archiveFile
    modLoaders.addAll("neoforge")
}