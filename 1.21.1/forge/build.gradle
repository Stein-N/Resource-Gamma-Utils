plugins {
    id 'ml-loader'
    id 'net.minecraftforge.gradle'
    id 'org.spongepowered.mixin' version '0.7-SNAPSHOT'
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
    implementation('net.sf.jopt-simple:jopt-simple:5.0.4') { version { strictly '5.0.4' } }
}

mixin {
    config("${mod_id}.mixins.json")
}

minecraft {
    mappings channel: 'official', version: minecraft_version
    reobf = false
    copyIdeResources = true //Calls processResources when in dev

    def directory = project.projectDir.parent.split("\\\\").last()

    def at = file('../common/src/main/resources/META-INF/accesstransformer.cfg')
    if (at.exists()) accessTransformers = [at]

    runs {
        client {
            workingDirectory file("../common/runs/client")
            ideaModule "${rootProject.name}.${directory}.${project.name}.main"
            taskName "Client"
            mods {
                modClientRun {
                    source sourceSets.main
                }
            }
        }
    }
}

jar {
    manifest {
        attributes([
                'FMLCorePluginContainsFMLMod': 'true',
                'MixinConfigs': "${mod_id}.mixins.json"
        ])
    }
}

sourceSets.each {
    def dir = layout.buildDirectory.dir("sourcesSets/$it.name")
    it.output.resourcesDir = dir
    it.java.destinationDirectory = dir
}

publishMods {
    file = jar.archiveFile
    modLoaders.addAll("forge")
}