plugins {
    id 'loader'
    id 'fabric-loom'
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.officialMojangMappings()

    modImplementation "net.fabricmc:fabric-loader:${fabric_loader}"
    modApi "net.fabricmc.fabric-api:fabric-api:${fabric_api}+${minecraft_version}"

    modImplementation "com.terraformersmc:modmenu:${mod_menu}"
    modImplementation "maven.modrinth:iris:1.8.8+1.21.1-fabric"
    modRuntimeOnly "maven.modrinth:sodium:mc1.21.1-0.6.13-fabric"
}

loom {
    mixin {
        defaultRefmapName.set("${mod_id}.refmap.json")
    }

    def aw = file("src/main/resources/${mod_id}.accesswidener")
    if (aw.exists()) accessWidenerPath.set(aw)

    runs {
        client {
            client()
            setConfigName("Fabric Client")
            ideConfigGenerated(true)
            programArgs('--username', 'Dev')
            runDir("../common/runs/client")
        }
    }
}


// Implement mcgradleconventions loader attribute
def loaderAttribute = Attribute.of('io.github.mcgradleconventions.loader', String)
['apiElements', 'runtimeElements', 'sourcesElements', 'javadocElements', 'includeInternal', 'modCompileClasspath'].each { variant ->
    configurations.named("$variant") {
        attributes {
            attribute(loaderAttribute, 'fabric')
        }
    }
}
sourceSets.configureEach {
    [it.compileClasspathConfigurationName, it.runtimeClasspathConfigurationName].each { variant->
        configurations.named("$variant") {
            attributes {
                attribute(loaderAttribute, 'fabric')
            }
        }
    }
}
loom.remapConfigurations.configureEach {
    configurations.named(it.name) {
        attributes {
            attribute(loaderAttribute, 'fabric')
        }
    }
}

publishMods {
    file = remapJar.archiveFile
    modLoaders.addAll("fabric", "quilt")
}